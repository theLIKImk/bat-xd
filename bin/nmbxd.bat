@echo off
chcp 65001
setlocal EnableDelayedExpansion
set XD_CORE_VER=0.0.4
set NA_DIR=%~dp0
set NA_TMP=%NA_DIR%TMP\
set NA_curl_HEAD=User-Agent: Mozilla/5.0 (Windows NT 10.0; Win64; x64)
set NA_Cookie=
set NA_sep=#n#
set NA_EXIT=0

if not exist "%na_tmp%" mkdir "%na_tmp%"

if /i "%1"=="getForumList" goto :getForumList
if /i "%1"=="showf" goto :showf
if /i "%1"=="thread" goto :thread
if /i "%1"=="notice" goto :nmb-notice
if /i "%1"=="help" goto :help
if /i "%1"=="send" goto :send

if /i "%NA_DEBUG%"=="true" goto :%1

exit /b

::todo

:image

:send
	set NA_send_name=
	set NA_send_title=
	set NA_send_fid=%2
	set NA_send_resto=%2
	set NA_send_content=
	set NA_send_Image_path=
	set NA_send_Image_watermark=True
	set NA_send_Image_cli=
	set NA_send_type=
	
	if "%2"=="" echo.ID为空 & exit /b 5
	if "%2" GTR "40000000" (
		set NA_send_type=t
	) else (
		set NA_send_type=s
	)
	
	if /i "!NA_send_type!"=="s" (
		call loadcfg "%NA_TMP%\platein.ini"
		set plate_id=!%2!
		call loadcfg "%NA_TMP%\plate_!plate_id!_!NA_send_fid!_msg.ini"
		echo.发送 ^>^> !NAME!板块
	) else (
		echo.发送 ^>^> NO.!NA_send_resto!
	)
	echo.
	
	set /p NA_send_title=标题:
	set /p NA_send_name=名字:
	set /p NA_send_Image_path=图片路径（不准带引号，为空不发送）:
	set /p NA_send_Image_watermark=是否添加水印[N/y]:
	set /p NA_send_content=正文内容:
	
	if /i "!NA_send_Image_watermark!"=="y" (
		set NA_send_Image_watermark=True
	) else (
		set NA_send_Image_watermark=False
	)
	if defined NA_send_Image_path (
		if not exist "!NA_send_Image_path!" echo 路径不存在！ & exit /b 6
		set NA_send_Image_cli=-F "image=@\"!NA_send_Image_path!\""
	)
	
	
	if /i "!NA_send_type!"=="s" (
		CALL :CURL-POST https://www.nmbxd.com/Home/Forum/doPostThread.html ^
-F "fid=!NA_send_fid!" ^
-F "title=\"!NA_send_title!\"" ^
-F "name=\"!NA_send_name!\"" ^
-F "content=\"!NA_send_content!\"" ^
-F "water=\"!NA_send_Image_watermark!\"" !NA_send_Image_cli!
	) else (
		CALL :CURL-POST https://www.nmbxd.com/Home/Forum/doReplyThread.html ^
-F "resto=\"!NA_send_resto!\"" ^
-F "title=\"!NA_send_title!\"" ^
-F "name=\"!NA_send_name!\"" ^
-F "content=\"!NA_send_content!\"" ^
-F "water=\"!NA_send_Image_watermark!\"" !NA_send_Image_cli!
	)
	
	echo 执行完毕
	
exit /b 0

:cookie
	if "%2"=="" echo cookie空[%*]！& exit /b 4
	echo.# Netscape HTTP Cookie File>"%~dp0/cookies.txt"
	echo.# https://curl.se/docs/http-cookies.html>>"%~dp0/cookies.txt"
	echo.# This file was generated by libcurl! Edit at your own risk.>>"%~dp0/cookies.txt"
	echo.# >>"%~dp0/cookies.txt"
	echo.# 饼干文件，自动生成! 生成时间：%data% %time%>>"%~dp0/cookies.txt"
	echo.>>"%~dp0/cookies.txt"
	echo.api.nmb.best	FALSE	/	FALSE	0	userhash	%2>>"%~dp0/cookies.txt"
	echo.www.nmbxd1.com	FALSE	/	FALSE	0	userhash	%2>>"%~dp0/cookies.txt"
	echo.www.nmbxd.com	FALSE	/	FALSE	0	userhash	%2>>"%~dp0/cookies.txt"
exit /b 0

:help
	echo.
	echo.cookie ^<userhash^>
	echo.getForumList
	echo.help
	echo.nmb-notice
	echo.send ^<id^>
	echo.showf ^<id^> [^<page^>]
	echo.thread ^<id^> [^<page^>]
	echo.
	echo.
	echo.ERRORLEVEL:
	echo.0	执行完毕
	echo.1	未指定ID
	echo.2	无权限访问
	echo.3	ID不存在
	echo.4  Cookie输入空
	echo.5  发送指定板块/ID空
	echo.6  指定路径不存在
	
exit /b


:nmb-notice
	::公告
	::
	set showfile=nmb-notice.txt
	DEL /f /s /q "%NA_tmp%nmb-notice*" >nul
	DEL /f /s /q "%NA_tmp%notice_content*" >nul
	call :curl-get http://nmb.ovear.info/nmb-notice.json --output "%NA_TMP%\nmb-notice.tmp"
	type "%NA_TMP%\nmb-notice.tmp" | jq > "%NA_TMP%\nmb-notice.json"
	
	for /f "delims=*" %%s in ('jj -i "%NA_TMP%\nmb-notice.json" content') do (echo.%%s>>"%NA_TMP%notice_content.txt")
	for /f "delims=*" %%s in ('jj -i "%NA_TMP%\nmb-notice.json" date') do (set notice_time=%%s)
	
	echo.	^|>>"%NA_TMP%\!showfile!"
	echo.	^|>>"%NA_TMP%\!showfile!"
	echo.	^|>>"%NA_TMP%\!showfile!"
	echo.	^|      NMDBXD   NOTICE>>"%NA_TMP%\!showfile!"
	echo.	^|>>"%NA_TMP%\!showfile!"
	echo.	^|>>"%NA_TMP%\!showfile!"
	echo.	^|>>"%NA_TMP%\!showfile!"
	echo.________^|__________________________________________________>>"%NA_TMP%\!showfile!"
	echo.	^|>>"%NA_TMP%\!showfile!"
	echo.	^|   ^| 人，是会思考的芦苇。>>"%NA_TMP%\!showfile!"
	echo.	^|   ^|              ——帕斯卡，《思想录》>>"%NA_TMP%\!showfile!"
	echo.	^|   ^|>>"%NA_TMP%\!showfile!"
	echo.	^|   ^| 开放包容 理性客观 有事说事 就事论事 顺猴者昌 逆猴者亡>>"%NA_TMP%\!showfile!"
	echo.	^|>>"%NA_TMP%\!showfile!"
	for /f "delims=*" %%s in (%NA_TMP%notice_content.txt) do (echo.	^| %%s>>"%NA_TMP%\!showfile!")
	echo.	^|>>"%NA_TMP%\!showfile!"
	echo.	^| DATE: !notice_time!>>"%NA_TMP%\!showfile!"
	echo.________^|__________________________________________________>>"%NA_TMP%\!showfile!"
	
	set notice_time=
exit /b 0

:thread
	:: 
	REM 串串
	::
	if "%2"=="" echo.未指定板块ID & set NA_EXIT=1 & goto :thread_end
	set thread_id=%2
	set thread_id_page=%3
	if not defined thread_id_page set thread_id_page=1
	set showfile=thread_!thread_id!_page_!thread_id_page!_list.txt
	
	DEL /f /s /q "%NA_tmp%thread_!thread_id!*" >nul
	
	REM 获取
	call :curl-get https://api.nmb.best/api/thread?id=!thread_id!#s#page=!thread_id_page! --output "%NA_TMP%\thread_!thread_id!.tmp"
	type "%NA_TMP%\thread_!thread_id!.tmp" | jq > "%NA_TMP%\thread_!thread_id!.json"

	REM 验证
	for /f "delims=*" %%s in ('jj -i "%NA_TMP%\thread_!thread_id!.json" id') do (set t_id=%%s)
	if not defined t_id (
		for /f "delims=*" %%s in ('jj -i "%NA_TMP%\thread_!thread_id!.json" success') do (set thread_success=%%s)
		if /i "!thread_success!"=="false" (
			echo 没有权限访问！
			set NA_EXIT=2
			goto :thread_end
		) else (
			echo 串不存在
			set NA_EXIT=3
			goto :thread_end
		)
	)
	
	for /f "delims=*" %%s in ('jj -i "%NA_TMP%\thread_!thread_id!.json" title') do (set po_title=%%s)
	for /f "delims=*" %%s in ('jj -i "%NA_TMP%\thread_!thread_id!.json" name') do (set po_name=%%s)
	for /f "delims=*" %%s in ('jj -i "%NA_TMP%\thread_!thread_id!.json" now') do (set po_now=%%s)
	for /f "delims=*" %%s in ('jj -i "%NA_TMP%\thread_!thread_id!.json" user_hash') do (set po_user_hash=%%s)
	for /f "delims=*" %%s in ('jj -i "%NA_TMP%\thread_!thread_id!.json" sage') do (set po_sage=%%s)
	for /f "delims=*" %%s in ('jj -i "%NA_TMP%\thread_!thread_id!.json" ReplyCount') do (set po_ReplyCount=%%s)
	for /f "delims=*" %%s in ('jj -i "%NA_TMP%\thread_!thread_id!.json" img') do (set po_img=%%s)
	for /f "delims=*" %%s in ('jj -i "%NA_TMP%\thread_!thread_id!.json" content') do (set po_content=%%s & echo.%%s>>"%NA_TMP%thread_!thread_id!_content.txt")
	
	set thread_po=!po_user_hash!
	
	::头部
	::
	echo.	^|>>"%NA_TMP%\!showfile!"
	echo.	^|>>"%NA_TMP%\!showfile!"
	echo.	^|>>"%NA_TMP%\!showfile!"
	echo.	^|      NMDBXD ^> !thread_id! >>"%NA_TMP%\!showfile!"
	echo.	^|>>"%NA_TMP%\!showfile!"
	echo.	^|      页码: !thread_id_page!>>"%NA_TMP%\!showfile!"
	echo.	^|>>"%NA_TMP%\!showfile!"
	echo.________^|__________________________________________________>>"%NA_TMP%\!showfile!"
	if "!po_sage!"=="1" (
		echo.!thread_id!^| !po_user_hash!    !po_now!    [PO]    [ SAGE ]>>"%NA_TMP%\!showfile!"
	) else (
		echo.!thread_id!^| !po_user_hash!    !po_now!    [PO]>>"%NA_TMP%\!showfile!"
	)
	echo.	^|>>"%NA_TMP%\!showfile!"
	echo.	^| 标题: !po_title!>>"%NA_TMP%\!showfile!"
	echo.	^| 名字: !po_name!>>"%NA_TMP%\!showfile!"
	if defined po_img echo.	^| [ IMAGE ]>>"%NA_TMP%\!showfile!"
	echo.	^| . . . . . . . . . . . . . . . . . . . . . . . . .>>"%NA_TMP%\!showfile!"
	echo.	^|>>"%NA_TMP%\!showfile!"
	for /f "delims=*" %%s in (%NA_TMP%thread_!thread_id!_content.txt) do (echo.	^| %%s>>"%NA_TMP%\!showfile!")
	echo.________^|__________________________________________________>>"%NA_TMP%\!showfile!"
	echo. NO	^| 回复 (!po_ReplyCount!)>>"%NA_TMP%\!showfile!"
	echo.________^|__________________________________________________>>"%NA_TMP%\!showfile!"
	
	::回复
	::
	for /l %%i in (0,1,!po_ReplyCount!) do (
		for /f "delims=*" %%s in ('jj -i "%NA_TMP%\thread_!thread_id!.json" Replies.%%i.id') do (set thread_replies_id=%%s)
		if not defined thread_replies_id goto :thread_end
		
		for /f "delims=*" %%s in ('jj -i "%NA_TMP%\thread_!thread_id!.json" Replies.%%i.user_hash') do (set thread_replies_user_hash=%%s)
		for /f "delims=*" %%s in ('jj -i "%NA_TMP%\thread_!thread_id!.json" Replies.%%i.title') do (set thread_replies_title=%%s)
		for /f "delims=*" %%s in ('jj -i "%NA_TMP%\thread_!thread_id!.json" Replies.%%i.name') do (set thread_replies_name=%%s)
		for /f "delims=*" %%s in ('jj -i "%NA_TMP%\thread_!thread_id!.json" Replies.%%i.content') do (set thread_replies_content=%%s & echo.%%s>>"%NA_TMP%thread_!thread_id!_replies_!thread_replies_id!.txt")
		for /f "delims=*" %%s in ('jj -i "%NA_TMP%\thread_!thread_id!.json" Replies.%%i.now') do (set thread_replies_now=%%s)
		for /f "delims=*" %%s in ('jj -i "%NA_TMP%\thread_!thread_id!.json" Replies.%%i.img') do (set thread_replies_img=%%s)
		
		if "!thread_replies_id!"=="9999999" (
			echo.*TIPS	!^| 捣乱的X岛提示娘>>"%NA_TMP%\!showfile!"
			echo.	^|>>"%NA_TMP%\!showfile!"
		) else (
			if "!thread_replies_user_hash!"=="!thread_po!" (
				echo.!thread_replies_id!^| !thread_replies_user_hash!    !thread_replies_now!    [ PO ]>>"%NA_TMP%\!showfile!"
			) else (
				echo.!thread_replies_id!^| !thread_replies_user_hash!    !thread_replies_now!>>"%NA_TMP%\!showfile!"
			)
			echo.	^|>>"%NA_TMP%\!showfile!"
			echo.	^| 标题: !thread_replies_title!>>"%NA_TMP%\!showfile!"
			echo.	^| 名字: !thread_replies_name!>>"%NA_TMP%\!showfile!"
			if defined thread_replies_img echo.	^| [ IMAGE ]>>"%NA_TMP%\!showfile!"
			echo.	^| . . . . . . . . . . . . . . . . . . . . . . . . .>>"%NA_TMP%\!showfile!"
			echo.	^|>>"%NA_TMP%\!showfile!"
		)
		for /f "delims=*" %%s in (%NA_TMP%thread_!thread_id!_replies_!thread_replies_id!.txt) do (echo.	^| %%s>>"%NA_TMP%\!showfile!")
		echo.________^|__________________________________________________>>"%NA_TMP%\!showfile!"
		set thread_replies_id=
	)
	
:thread_end
	set thread_id=
	set thread_id_page=
	set thread_success=
	set thread_replies_user_hash=
	set thread_replies_name=
	set thread_replies_now=
	set thread_replies_img=
	set po_user_hash=
	set po_name=
	set po_title=
	set po_content=
	set po_now=
	set po_ReplyCount=
	set po_sage=
exit /b !NA_EXIT!

:showf
	REM 板块条目
	if "%2"=="" echo.未指定板块ID & set NA_EXIT=1 & goto :out_showf
	set showf_id=%2
	set showf_id_page=%3
	if not defined showf_id_page set showf_id_page=1
	set showfile=showf_id_!showf_id!_page_!showf_id_page!_list.txt

	del /f /s /q "%NA_TMP%\showf_id_!showf_id!*" >NUL
	del /f /s /q "%NA_TMP%\po_*_content.txt" >NUL
	
	REM 时间线处理
	if /i "!showf_id:~0,1!"=="T" (
		call :curl-get https://api.nmb.best/api/timeline?id=!showf_id:~1,-1!#S#page=!showf_id_page! --output "%NA_TMP%\showf_id_!showf_id!.tmp"
		type "%NA_TMP%\showf_id_!showf_id!.tmp" | jq > "%NA_TMP%\showf_id_!showf_id!.json"
	
	) else (
		call :curl-get https://api.nmb.best/api/showf?id=!showf_id!#S#page=!showf_id_page! --output "%NA_TMP%\showf_id_!showf_id!.tmp"
		type "%NA_TMP%\showf_id_!showf_id!.tmp" | jq > "%NA_TMP%\showf_id_!showf_id!.json"
	)
	
	for /f "delims=*" %%s in ('jj -i "%NA_TMP%\showf_id_!showf_id!.json" success') do (set showf_success=%%s)
	if /i "!showf_success!"=="false" echo 无权限访问 & set NA_EXIT=2 & goto :out_showf
	for /f "delims=*" %%s in ('jj -i "%NA_TMP%\showf_id_!showf_id!.json" 0.id') do (set showf_exist=%%s)
	if not defined showf_exist echo ID不存在 & set NA_EXIT=3 & goto :out_showf
	
	call loadcfg "%NA_TMP%\platein.ini"
	set plate_id=!%2!
	call loadcfg "%NA_TMP%\plate_!plate_id!_!showf_id!_msg.ini"
	
	echo.	^|>>"%NA_TMP%\!showfile!"
	echo.	^|>>"%NA_TMP%\!showfile!"
	echo.	^|>>"%NA_TMP%\!showfile!"
	echo.	^|      NMDBXD ^> !NAME!>>"%NA_TMP%\!showfile!"
	echo.	^|>>"%NA_TMP%\!showfile!"
	echo.	^|      页码: !showf_id_page!>>"%NA_TMP%\!showfile!"
	echo.	^|>>"%NA_TMP%\!showfile!"
	if exist "%NA_TMP%plate_!plate_id!_!showf_id!_msg.txt" (
		for /f "delims=*" %%s in (%NA_TMP%plate_!plate_id!_!showf_id!_msg.txt) do (echo.	^|      %%s>>"%NA_TMP%\!showfile!")
	) else (
		echo.	^|      >>"%NA_TMP%\!showfile!"
	)
	echo.	^|>>"%NA_TMP%\!showfile!"
	echo.________^|__________________________________________________>>"%NA_TMP%\!showfile!"
	echo. NO	^| 条目>>"%NA_TMP%\!showfile!"
	echo.________^|__________________________________________________>>"%NA_TMP%\!showfile!"
	
	REM 条目处理
	for /l %%i in (0,1,9999) do (
		for /f "delims=*" %%s in ('jj -i "%NA_TMP%\showf_id_!showf_id!.json" %%i.id') do (set po_id=%%s)
		if not defined po_id goto :out_showf
		
		for /f "delims=*" %%s in ('jj -i "%NA_TMP%\showf_id_!showf_id!.json" %%i') do (echo.%%s>>"%NA_TMP%\showf_id_!showf_id!_po_!po_id!.json")
		
		for /f "delims=*" %%s in ('jj -i "%NA_TMP%\showf_id_!showf_id!_po_!po_id!.json" user_hash') do (set po_user_hash=%%s)
		for /f "delims=*" %%s in ('jj -i "%NA_TMP%\showf_id_!showf_id!_po_!po_id!.json" name') do (set po_name=%%s)
		for /f "delims=*" %%s in ('jj -i "%NA_TMP%\showf_id_!showf_id!_po_!po_id!.json" title') do (set po_title=%%s)
		for /f "delims=*" %%s in ('jj -i "%NA_TMP%\showf_id_!showf_id!_po_!po_id!.json" content') do (set po_content=%%s & echo %%s>>"%NA_tmp%po_!po_id!_content.txt")
		for /f "delims=*" %%s in ('jj -i "%NA_TMP%\showf_id_!showf_id!_po_!po_id!.json" now') do (set po_now=%%s)
		for /f "delims=*" %%s in ('jj -i "%NA_TMP%\showf_id_!showf_id!_po_!po_id!.json" ReplyCount') do (set po_ReplyCount= %%s)
		for /f "delims=*" %%s in ('jj -i "%NA_TMP%\showf_id_!showf_id!_po_!po_id!.json" sage') do (set po_sage=%%s)
		for /f "delims=*" %%s in ('jj -i "%NA_TMP%\showf_id_!showf_id!_po_!po_id!.json" img') do (set po_img=%%s)
		
		if "!po_sage!"=="1" (
			echo.!po_id!^| !po_user_hash!    !po_now!    [ SAGE ]>>"%NA_TMP%\!showfile!" 
		) else (
			echo.!po_id!^| !po_user_hash!    !po_now!>>"%NA_TMP%\!showfile!" 
		)
		echo.	^|>>"%NA_TMP%\!showfile!" 
		echo.	^| 标题：!po_title!>>"%NA_TMP%\!showfile!" 
		echo.	^| 名字：!po_name!>>"%NA_TMP%\!showfile!" 
		echo.	^| 回复：!po_ReplyCount!个>>"%NA_TMP%\!showfile!" 
		if defined po_img echo.	^| [ IMAGE ]>>"%NA_TMP%\!showfile!" 
		echo.	^| . . . . . . . . . . . . . . . . . . . . . . . . .>>"%NA_TMP%\!showfile!" 
		echo.	^|>>"%NA_TMP%\!showfile!" 
		for /f "delims=*" %%s in (%NA_TMP%po_!po_id!_content.txt) do (echo.	^| %%s>>"%NA_TMP%\!showfile!")
		echo.	^| . . . . . . . . . . . . . . . . . . . . . . . . .>>"%NA_TMP%\!showfile!" 
		
		REM 简要回复预览
		call :showf_ReplyCount
		
		
		echo.________^|__________________________________________________>>"%NA_TMP%\!showfile!"
		
		set po_id=
		set po_user_hash=
		set po_name=
		set po_title=
		set po_content=
		set po_now=
		set po_ReplyCount=
		set po_sage=
		set po_img=
	)
	:out_showf
	set po_id=
	set po_user_hash=
	set po_name=
	set po_title=
	set po_content=
	set po_now=
	set po_ReplyCount=
	set po_sage=
	set po_img=
	set showf_exist=
	set showf_success=
exit /b !NA_EXIT!

:showf_ReplyCount
	REM 为了一致性.......
	for /l %%j in (0,1,9999) do (
		for /f "delims=*" %%s in ('jj -i "%NA_TMP%\showf_id_!showf_id!_po_!po_id!.json" Replies.%%j.id') do (set po_reply_id=%%s)
		if not defined po_reply_id goto :out_showf_ReplyCount
		
		for /f "delims=*" %%s in ('jj -i "%NA_TMP%\showf_id_!showf_id!_po_!po_id!.json" Replies.%%j.IMG') do (set po_reply_img=%%s)
		for /f "delims=*" %%s in ('jj -i "%NA_TMP%\showf_id_!showf_id!_po_!po_id!.json" Replies.%%j.user_hash') do (set po_reply_user_hash=%%s)
		for /f "delims=*" %%s in ('jj -i "%NA_TMP%\showf_id_!showf_id!_po_!po_id!.json" Replies.%%j.content') do (set po_reply_content=%%s & echo %%s>>"%NA_tmp%po_!po_reply_id!_reply_content.txt")
		
		set /p po_reply_head_line=<"%NA_tmp%po_!po_reply_id!_reply_content.txt"
		
		if defined po_reply_img (
			echo.	^|  [ NO!po_reply_id! ^| !po_reply_user_hash! ] ^(I^) !po_reply_head_line!>>"%NA_TMP%\!showfile!" 
		) else (
			echo.	^|  [ NO!po_reply_id! ^| !po_reply_user_hash! ] !po_reply_head_line!>>"%NA_TMP%\!showfile!" 
		)
		for /f "skip=1 delims=*" %%s in (%NA_TMP%po_!po_reply_id!_reply_content.txt) do (echo.	^|                           %%s>>"%NA_TMP%\!showfile!")
		
		set po_reply_id=
		set po_reply_user_hashs=
		set po_reply_content=
		set po_reply_head_line=
		set po_reply_img=
	)
	:out_showf_ReplyCount
exit /b


:getForumList
	REM 板块列表
	set showfile=platelist.txt
	
	DEL /F /S /Q "%NA_TMP%\ForumList.*" >NUL 2>NUL
	DEL /F /S /Q "%NA_TMP%\TimelineList.*" >NUL 2>NUL
	DEL /F /S /Q "%NA_TMP%\parent_section*" >NUL 2>NUL
	DEL /F /S /Q "%NA_TMP%\plate*.ini" >NUL 2>NUL
	DEL /F /S /Q "%NA_TMP%\plate*msg.txt" >NUL 2>NUL
	DEL /F /S /Q "%NA_TMP%\!showfile!" >NUL 2>NUL
	DEL /F /S /Q "%NA_TMP%\platein.ini" >NUL 2>NUL
	

	call :curl-get https://api.nmb.best/api/getForumList --output "%NA_TMP%\ForumList.tmp"
	call :curl-get https://api.nmb.best/api/getTimelineList --output "%NA_TMP%\TimelineList.tmp"
	type "%NA_TMP%\ForumList.tmp" | jq > "%NA_TMP%\ForumList.json"
	type "%NA_TMP%\TimelineList.tmp" | jq > "%NA_TMP%\TimelineList.json"
	
	echo.	^|>>"%NA_TMP%\!showfile!"
	echo.	^|>>"%NA_TMP%\!showfile!"
	echo.	^|>>"%NA_TMP%\!showfile!"
	echo.	^|      WELCOME TO NMDBXD! >>"%NA_TMP%\!showfile!"
	echo.	^|>>"%NA_TMP%\!showfile!"
	echo.	^|>>"%NA_TMP%\!showfile!"
	echo.	^|>>"%NA_TMP%\!showfile!"
	echo.________^|__________________________________________________>>"%NA_TMP%\!showfile!"
	echo. ID	^| 板块>>"%NA_TMP%\!showfile!"
	echo.________^|__________________________________________________>>"%NA_TMP%\!showfile!"
	
	REM 时间线
	echo.    	^|  [[[[ 时间线 ]]]] >>"%NA_TMP%\!showfile!"
	
	echo.[0] >>"%NA_TMP%\plate.ini"
	echo.name=时间线 >>"%NA_TMP%\plate.ini"
	
	copy "%NA_TMP%\TimelineList.json" "%NA_TMP%\parent_section_0.json"
	for /l %%j in (0,1,9999) do (
		for /f "delims=*" %%s in ('jj -i "%NA_TMP%\parent_section_0.json" %%j.id') do (set subsection_id=%%s)
		if not defined subsection_id goto out_parent_section_timeline
		for /f "delims=*" %%s in ('jj -i "%NA_TMP%\parent_section_0.json" %%j.name') do (set subsection_name=%%s)
		for /f "delims=*" %%s in ('jj -i "%NA_TMP%\parent_section_0.json" %%j.notice') do (set subsection_msg=%%s & echo.%%s>>"%NA_tmp%plate_0_T!subsection_id!_msg.txt")
		
		set subsection_msg=!subsection_msg:^&=#s#!
		set subsection_msg=!subsection_msg:^>=#\#!
		set subsection_msg=!subsection_msg:^<=#/#!
		echo.[T!subsection_id!] >>"%NA_TMP%\plate_0.ini"
		echo.name=!subsection_name! >>"%NA_TMP%\plate_0.ini"
		echo.msg=!subsection_msg! >>"%NA_TMP%\plate_0.ini"
	
		echo.T!subsection_id!=^0>>"%NA_TMP%\platein.ini"
			
		echo.NAME=!subsection_name!>>"%NA_TMP%\plate_0_T!subsection_id!_msg.ini"
		echo.MSG=!subsection_msg!>>"%NA_TMP%\plate_0_T!subsection_id!_msg.ini"
		
		echo.T!subsection_id!	^|   - !subsection_name! >>"%NA_TMP%\!showfile!"
		
		set subsection_id=
		set subsection_name=
		set subsection_msg=
	)
	:out_parent_section_timeline
	
	
	REM 除开时间线之外
	for /l %%i in (0,1,9999) do (
	
		REM 父板块
		for /f "delims=*" %%s in ('jj -i "%NA_TMP%\ForumList.json" %%i.id') do (set parent_section_id=%%s)
		for /f "delims=*" %%s in ('jj -i "%NA_TMP%\ForumList.json" %%i.name') do (set parent_section_name=%%s)
		for /f "delims=*" %%s in ('jj -i "%NA_TMP%\ForumList.json" %%i') do (echo %%s>>"%NA_TMP%\parent_section_!parent_section_id!.json")
		if not defined parent_section_id goto out_parent_section
		
		echo.[!parent_section_id!] >>"%NA_TMP%\plate.ini"
		echo.name=!parent_section_name! >>"%NA_TMP%\plate.ini"
		
		echo.    	^|  [[[[ !parent_section_name! ]]]] >>"%NA_TMP%\!showfile!"
		
		REM 子版块和剩余部分
		call :getForumList_subsection
		
		set parent_section=
		set parent_section_id=
	)
	:out_parent_section
exit /b 0

:getForumList_subsection
	REM FUCK!
	for /l %%j in (0,1,9999) do (
		for /f "delims=*" %%s in ('jj -i "%NA_TMP%\parent_section_!parent_section_id!.json" forums.%%j.id') do (set subsection_id=%%s)
		if not defined subsection_id goto out_subsection
		
		for /f "delims=*" %%s in ('jj -i "%NA_TMP%\parent_section_!parent_section_id!.json" forums.%%j.name') do (set subsection_name=%%s)
		for /f "delims=*" %%s in ('jj -i "%NA_TMP%\parent_section_!parent_section_id!.json" forums.%%j.msg') do (set subsection_msg=!subsection_msg! & echo.%%s>>"%NA_tmp%plate_!parent_section_id!_!subsection_id!_msg.txt")
		
		if defined subsection_id (
			set subsection_msg=!subsection_msg:^&=#s#!
			set subsection_msg=!subsection_msg:^>=#\#!
			set subsection_msg=!subsection_msg:^<=#/#!
			echo.[!subsection_id!] >>"%NA_TMP%\plate_!parent_section_id!.ini"
			echo.name=!subsection_name! >>"%NA_TMP%\plate_!parent_section_id!.ini"
			echo.msg=!subsection_msg! >>"%NA_TMP%\plate_!parent_section_id!.ini"
			
			echo.!subsection_id!=!parent_section_id!>>"%NA_TMP%\platein.ini"
			
			echo.NAME=!subsection_name!>>"%NA_TMP%\plate_!parent_section_id!_!subsection_id!_msg.ini"
			echo.MSG=!subsection_msg!>>"%NA_TMP%\plate_!parent_section_id!_!subsection_id!_msg.ini"
			
			echo.!subsection_id!	^|   - !subsection_name! >>"%NA_TMP%\!showfile!"
		)
		
		set subsection_id=
		set subsection_name=
		set subsection_msg=
	)
	echo.________^|__________________________________________________>>"%NA_TMP%\!showfile!"
	:out_subsection
exit /b

:curl-get
	set val=%*
	set val=%val:#S#=^^^&%
	curl -X GET -L --compressed -H "%NA_curl_HEAD%" -b cookies.txt %val%
exit /b

:curl-post
	set val=%*
	set val=%val:#S#=^^^&%
	curl -X POST -L --compressed -H "%NA_curl_HEAD%" -b cookies.txt %val%
exit /b
